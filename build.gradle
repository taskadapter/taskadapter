ext {
    vaadinVersion = '14.4.7'
    vaadinFlowServerVersion = '5.0.2'
    httpClientVersion='4.2'
    slf4jVersion = '1.7.12'
}

ext.libraries = [
    gson: 'com.google.code.gson:gson:1.6',
    httpclient: "org.apache.httpcomponents:httpclient:$httpClientVersion",
    httpcore: "org.apache.httpcomponents:httpcore:$httpClientVersion",
    servletAPI: "javax.servlet:javax.servlet-api:3.0.1",
    commonsCodec: "commons-codec:commons-codec:1.6",
    slf4j: "org.slf4j:slf4j-api:$slf4jVersion",
    slf4jBindingLibrary: "org.slf4j:slf4j-log4j12:$slf4jVersion",
    log4j: "log4j:log4j:1.2.17",
    junit: "junit:junit:4.13.2",
    assertJ   : "org.assertj:assertj-core:3.18.0",
    vaadinCore: [dependencies.create("com.vaadin:vaadin-core") {
//         Webjars are only needed when running in Vaadin 13 compatibility mode
        ["com.vaadin.webjar", "org.webjars.bowergithub.insites",
         "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
         "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
                .forEach { group -> exclude(group: group) }
    }]
]

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 14
    targetCompatibility = 14

    repositories {
        mavenLocal()
        maven {
            // note - cannot use https://dev.taskadapter.com/nexus/content/groups/public url for now
            // because Nginx->Nexus requires additional configuration for that.
            url 'http://dev.taskadapter.com:8081/nexus/repository/public'
        }
        maven {
            url 'https://maven.vaadin.com/vaadin-addons'
        }
        // This repository contains Atlassian JIRA REST Java Client library used by JIRA connector.
        // Browse artifacts online: https://maven.atlassian.com/content/groups/public/com/atlassian/jira/jira-rest-java-client-core/
        maven {
            url 'https://m2proxy.atlassian.com/repository/public'
        }
        mavenCentral()
    }

    dependencies {
        compile 'com.google.guava:guava:19.0'
        testCompile libraries.junit
    }

    // disabled javadoc generation during Gradle build to avoid build failure.
    // see https://github.com/johndevs/gradle-vaadin-plugin/issues/130
    javadoc {
        enabled = false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
    task allDeps(type: DependencyReportTask) {}
}


